// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Put your code here:
    //判斷輸入是否為A instruction[15] == 0>> A      instruction[15] == 1>> C
    Mux16(a=instruction, b=AluOP, sel=instruction[15], out=MUX0);

    //載入A暫存器
    //如果是A, load1 = 1
    //如果是C AND instruction[5] == 1, load1 = 1

    And(a=instruction[5], b=instruction[15], out=and1);
    Not(in=instruction[15], out=not1);
    Or(a=not1, b=and1, out=load1);

    //儲存地址在A暫存器

    ARegister(in=MUX0, load=load1, out=A, out[0..14]=addressM);

    //instruction[12] = a
    // 如果 a=1, 讀取 A (A暫存器)
    // 如果 a=0, 讀取 M (RAM[A])

    Mux16(a=A, b=inM, sel=instruction[12], out=MUX1);

    //載入D暫存器
    //C指令 AND d2 == 1, load2 = 1

    And(a=instruction[15], b=instruction[4], out=load2);
    DRegister(in=AluOP, load=load2, out=D);

    //ALU

    ALU(x=D, y=MUX1, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=outM, out=AluOP, zr=zr, ng=ng);

    //如果A AND d3 == 1, writeM = 1

    And(a=instruction[15], b=instruction[3], out=writeM);
    Or(a=zr, b=ng, out=zrORng); //輸入0或負數
    Not(in=zrORng, out=GT); //輸入非0或非負數(greater than)
    And(a=GT, b=instruction[0], out=JGT);
    And(a=zr, b=instruction[1], out=JEQ);
    Or(a=JEQ, b=JGT, out=JGE);
    And(a=ng, b=instruction[2], out=JLT);
    Or(a=JLT, b=JGE, out=Pass);
    And(a=instruction[15], b=Pass, out=pcjump);

    //PCload()=是C指令 AND 滿足上面條件>>JUMP

    PC(in=A, load=pcjump, inc=true, reset=reset, out[0..14]=pc);
}